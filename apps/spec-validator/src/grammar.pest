WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }

// Numbers
integer = @{ ("-")? ~ ASCII_DIGIT+ }
float   = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

// Booleans
boolean = @{ "true" | "false" }

// Strings
string = @{ ("\"" ~ (!"\"" ~ ANY)* ~ "\"") | ("'" ~ (!"'" ~ ANY)* ~ "'") }

// Identifiers
identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }

// Subjects
wildcard_subject = { "*" }
subject          = { boolean | float | integer | string | identifier | wildcard_subject }

// Subject selectors
evaluated_marker   = { "?" }
property_separator = { "/" }
subject_selector   = { evaluated_marker? ~ subject ~ (property_separator ~ subject)? }

// Assignment operator
operator            = _{ equals_operator | assignment_operator }
assignment_operator =  { "=" }
equals_operator     =  { "==" }

// Meta subjects
meta_prefix  = { "#" }
meta_subject = { meta_prefix ~ subject }
meta_list    = { meta_subject ~ ("," ~ meta_subject)* }

// Subject mapping
subject_mapping_separator = { ";;" }
subject_mapping_key       = { "subject" }
subject_mapping           = { subject_mapping_separator ~ subject_mapping_key ~ "=" ~ subject }

// Explain hint
explain_hint = { "!explain" }

// Facts - SUBJECT/PROPERTY = VALUE
fact = { subject_selector ~ operator ~ subject_selector ~ meta_list? ~ subject_mapping? ~ explain_hint? }

// Query syntax
query_marker    = { ">" }
expected_marker = { ">>" }
query_line      = { query_marker ~ subject_selector }
expected_line   = { expected_marker ~ fact }
expected_empty  = { expected_marker ~ "(no results)" }

// Explain syntax
explain_marker      =  { "=>" }
explain_for_keyword =  { "explain for" }
explain_header      =  { explain_marker ~ explain_for_keyword ~ subject }
explain_text_line   = @{ (!(NEWLINE ~ explain_marker) ~ !EOI ~ ANY)+ }
explain_block       =  { explain_header ~ NEWLINE ~ explain_text_line }

// Test structure
test_line = { explain_block | fact | query_line | expected_line | expected_empty }
test_file = { SOI ~ (test_line ~ NEWLINE*)* ~ EOI }
