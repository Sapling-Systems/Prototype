WHITESPACE = _{ " " | "\t" | "\r" }
COMMENT    = _{ "//" ~ (!"\n" ~ ANY)* }

// Numbers
integer = @{ ("-")? ~ ASCII_DIGIT+ }
float   = @{ ("-")? ~ ASCII_DIGIT+ ~ "." ~ ASCII_DIGIT+ }

// Booleans
boolean = @{ "true" | "false" }

// Strings
string = @{ ("\"" ~ (!"\"" ~ ANY)* ~ "\"") | ("'" ~ (!"'" ~ ANY)* ~ "'") }

// Identifiers
identifier          = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
fact_ref_identifier = @{ "@" ~ identifier }

// Subjects
wildcard_subject = { "*" }
subject          = { boolean | float | integer | string | fact_ref_identifier | identifier | wildcard_subject }

// Subject selectors
evaluated_marker   = { "?" }
property_separator = { "/" }
subject_selector   = { evaluated_marker? ~ subject ~ (property_separator ~ subject)? }

// Assignment operator
operator            = _{ equals_operator | assignment_operator }
assignment_operator =  { "=" }
equals_operator     =  { "==" }

// Meta subjects
meta_prefix  = { "#" }
meta_subject = { meta_prefix ~ subject }
meta_list    = { meta_subject ~ ("," ~ meta_subject)* }

// Subject mapping
subject_mapping_separator = { ";;" }
subject_mapping_key       = { "subject" }
subject_mapping           = { subject_mapping_separator ~ subject_mapping_key ~ "=" ~ subject }

// Fact identifier
fact_identifier = { fact_ref_identifier }

// Facts - SUBJECT/PROPERTY = VALUE
fact = { subject_selector ~ operator ~ subject_selector ~ meta_list? ~ subject_mapping? ~ fact_identifier? }

// Query syntax
query_marker            = { ">" }
expected_marker         = { ">>" }
expected_explain_marker = { "#>" }
query_line              = { query_marker ~ subject_selector }
expected_line           = { expected_marker ~ fact }
expected_empty          = { expected_marker ~ "(no results)" }
expected_explain_line   = { expected_explain_marker ~ (ASCII_ALPHA | ASCII_DIGIT | " " | "_" | "=" | "/" | ">" | ":" | "[" | "]" | "*" | "?" | "(" | ")" | "\"")* }

// Test structure
test_line = { fact | query_line | expected_line | expected_empty | expected_explain_line }
test_file = { SOI ~ (test_line ~ NEWLINE*)* ~ EOI }
